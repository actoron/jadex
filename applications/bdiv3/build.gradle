description 'The Jadex BDI V3 applications package contains several example applications, benchmarks and testcases using BDI V3 agents.'

// Test a specific java version, if set. 
if(Boolean.TRUE.equals(project.hasProperty('v3java')))
{
	def	version	= project.getProperty('v3java');
	sourceCompatibility = "1.$version"
	project.buildDir	= new File(project.buildDir, "buildJava$version")
}

// If java version is unset -> test ASM with different versions
else
{
	// Add extra test task for each desired java version
	['8', '11'].each { version ->
		// Create tasks as extra gradle builds to allow for different build output dirs
		// https://stackoverflow.com/questions/26459067/how-to-run-gradle-script-from-gradle
		// https://discuss.gradle.org/t/replacement-for-task-execute/27545
		def t	= task "testJava$version"(type: GradleBuild) {
			tasks = ["test"]
			buildName = "testJava$version"
			startParameter.projectProperties << ["v3java": version]
		}
		//test.dependsOn t
	}
	
	// Skip default test (redundant)
	//test.enabled false
}

dependencies
{
	api project(':util:commons')
	api project(':util:gui')
	api project(':platform:bridge')
	api project(':tools:base-swing')
	api project(':kernels:bdiv3')
	api project(':environments:envsupport')
	api project(':serialization:xml')
	api project(':kernels:application')
	
	testImplementation project(':platform:base')
	testImplementation project(':transports:websocket')
	testImplementation project(':transports:relay')
	testImplementation project(':transports:intravm')

 	// Allow starting examples from Main.java without extra dependencies
 	runtimeOnly project(':distributions:minimal')
	
	api 'com.toedter:jcalendar:1.3.2'
	api 'de.huxhorn.sulky:de.huxhorn.sulky.3rdparty.jlayer:1.0'
	
	api 'com.google.code.findbugs:annotations:3.0.0'	// For suppressing warnings for uninitialized fields
}
